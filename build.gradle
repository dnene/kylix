buildscript {
    ext.kotlin_version = "1.0.1-2"
    ext.dokka_version = "0.9.7"
    ext.gradle_bintray_version = "1.6"
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:${gradle_bintray_version}"
    }
}
group 'org.kotyle'
version '0.0.2-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.jfrog.bintray'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

ext {
    junit_version = '4.12'
}

sourceCompatibility = 1.6

dokka {
    moduleName = 'kylix'
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
    samples = ['samples/basic.kt']
}

bintray {
    user = BINTRAY_USER
    key = BINTRAY_API_KEY
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'org.kotyle'
        name = 'kylix'
        desc = "A collection of functional helper classes for Kotlin"
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/dnene/kylix.git'
        issueTrackerUrl = 'https://github.com/dnene/kylix/issues'
        labels = ['kotlin', 'functional']
        publicDownloadNumbers = true
        version {
            name = "${project.version}"
            desc = "Very early release ${project.version}, under continuing development"
            vcsTag = "${project.version}"
        }
    }
}

repositories {
    mavenCentral()
}


dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    testCompile "junit:junit:${junit_version}"
}
